plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'idea'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}

group 'com.betomorrow.gradle'
version '1.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    notSnapshot = { !version.endsWith("SNAPSHOT") }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compileOnly gradleApi()

    // Third Party
    compile 'com.google.api-client:google-api-client:1.27.0'
    compile 'com.google.oauth-client:google-oauth-client-jetty:1.27.0'
    compile 'com.google.apis:google-api-services-drive:v3-rev136-1.25.0'
    compile 'org.apache.poi:poi-ooxml:4.0.1'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    testCompile 'org.assertj:assertj-core:3.11.1'
    testCompile gradleTestKit()
    testCompile 'commons-io:commons-io:2.6'

    testRuntime 'cglib:cglib-nodep:3.2.4'       // allows mocking of classes (in addition to interfaces)
    testRuntime 'org.objenesis:objenesis:2.4'   // allows mocking of classes without default constructor (together with CGLIB)
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}


/**
 * Integration Tests
 */

sourceSets {
    integrationTest {
        java.srcDirs = [ file('src/integration-test/java') ]
        kotlin.srcDirs = [ file('src/integration-test/kotlin') ]
        resources.srcDirs = [ file('src/integration-test/resources') ]

        compileClasspath += sourceSets.main.output + configurations.integrationTestCompile
        runtimeClasspath += output + sourceSets.main.output + configurations.integrationTestRuntime
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

compileIntegrationTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task integrationTest(type: Test, group: JavaBasePlugin.VERIFICATION_GROUP) {
    description = 'Run the integration tests.'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    outputs.upToDateWhen { false }
    mustRunAfter test

    useJUnitPlatform()
}

check.dependsOn integrationTest

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.srcDirs
        testSourceDirs += sourceSets.integrationTest.kotlin.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs

        scopes.TEST.plus += [ configurations.integrationTestCompile, configurations.integrationTestRuntime, configurations.integrationTestAnnotationProcessor ]
    }
}


/**
 * Publishing
 */

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

gradlePlugin {
    testSourceSets sourceSets.integrationTest

    plugins {
        wordingPlugin {
            id = 'com.betomorrow.dotnet.wording'
            displayName = 'Gradle Wording Plugin for .Net Projects'
            description = 'Plugin to import wording from Google Sheet then integrate it as .resx files in .Net Projects.'
            implementationClass = 'com.betomorrow.gradle.wording.WordingPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/gmarrot/dotnet-wording-plugin'
    vcsUrl = 'https://github.com/gmarrot/dotnet-wording-plugin'

    tags = [ 'dotnet', 'wording', 'resx', 'tool' ]
}

publishPlugins {
    onlyIf notSnapshot
}


/**
 * Gradle Wrapper
 */

wrapper {
    gradleVersion = '5.5.1'
    distributionType = Wrapper.DistributionType.ALL
}